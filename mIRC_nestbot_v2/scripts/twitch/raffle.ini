[script]
n0=ON BOSS:TEXT:!raffle*:%twitch.chan:{
n1=  ; RAFFLE START
n2=  if ($int($2) == $2) { raffleStart $2- }
n3=  ; RAFFLE CLEAR
n4=  if ($2 == clear) { raffleClear }
n5=  ; RAFFLE STOP
n6=  if ($2 == stop) { raffleStop }
n7=  ; RAFFLE DRAW
n8=  if ($2 == draw) { raffleDraw %raffle.truelist }
n9=}
n10=
n11=ON *:TEXT:!ticket*:%twitch.chan:{ ticket $nick $2 }
n12=
n13=alias raffleClear { unset %raffle.* }
n14=
n15=alias raffleStart {
n16=  ; PARAMETRE NON NUMERIQUE
n17=  if (($int($1) != $1) || ($int($2) != $2)) { dire [RAFFLE] !raffle <prix> <ticket max> | return false }
n18=
n19=  ; CLEAR LA RAFFLE PRECEDENTE ET CREER LES VARIABLE PUIS ANNONCE
n20=  raffleClear
n21=  set %raffle.status 1
n22=  set %raffle.max $2
n23=  set %raffle.gils $1
n24=  set %raffle.reward.xppercent 0.2
n25=  set %raffle.reward.gils 100
n26=  dire [RAFFLE] La raffle démarre, tapez !ticket <nombre de ticket> pour avoir une chance d'être tiré au sort. Maximum %raffle.max tickets par viewer. 
n27=}
n28=
n29=alias ticket {
n30=  if (($int($2) != $2) || (%raffle.status != 1)) { return false }
n31=  var %target = $1
n32=  addViewer $1
n33=
n34=  ; AJOUTE LE VIEWER A LA LISTE SI IL N'Y EST PAS
n35=  if (%raffle.viewer. [ $+ [ %target ] ] == $null) { set %raffle.list $iif(%raffle.list == $null, %target, %raffle.list %target) }
n36=
n37=  ; REGLE LES TICKET
n38=  set %raffle.viewer. [ $+ [ %target ] ] $int($2)
n39=}
n40=
n41=alias raffleStop {
n42=  if (%raffle.status != 1) { dire [RAFFLE] Aucune tombola ouverte. | return false }
n43=  set %raffle.status 2
n44=  unset %raffle.truelist
n45=  verifTicket %raffle.list
n46=  dire [RAFFLE] Un total de %raffle.total.ticket ont était acheté pour la somme total de %raffle.total.gils gils.
n47=  raffleDraw %raffle.truelist
n48=  saveini
n49=}
n50=
n51=alias verifTicket {
n52=  var %pos = 0
n53=  set %raffle.total.ticket 0
n54=  set %raffle.total.gils 0
n55=
n56=  while (true) {
n57=    ; INCREMENTE LA POSITION ET STOP SI Y A PLUS PERSONNE
n58=    inc %pos
n59=
n60=    ; REGLE LE TARGET OU STOP SI AUCUNE
n61=    if ($ [ $+ [ %pos ] ] == $null) { break }
n62=    else { var %target = $ [ $+ [ %pos ] ] }
n63=
n64=    ; OUVRE LA CIBLE
n65=    addViewer %target
n66=
n67=    ; TICKET > 0 ONLY
n68=    if (%raffle.viewer. [ $+ [ %target ] ] != 0) {
n69=
n70=      ; EMPECHE LE DEPACEMENT DE TICKET MAX AUTORIsé
n71=      if (%raffle.viewer. [ $+ [ %target ] ] > %raffle.max) { set %raffle.viewer. [ $+ [ %target ] ] %raffle.max }
n72=
n73=      ; CALCULE LE PRIX POUR DONNER SES TICKET AU VIEWER
n74=      var %prix = $calc($int(%raffle.viewer. [ $+ [ %target ] ]) * $int(%raffle.gils))
n75=
n76=      ; PRIX TROP ELEVé ON DONNE LE MAX TICKET AUTORISé ET RECALCUL LE PRIX
n77=      if (%prix > %profile.gils. [ $+ [ %target ] ]) {
n78=        set %raffle.viewer. [ $+ [ %target ] ] $floor($calc($int(%profile.gils. [ $+ [ %target ] ]) / $int(%raffle.gils)))
n79=        var %prix = $calc($int(%raffle.viewer. [ $+ [ %target ] ]) * $int(%raffle.gils))
n80=      }
n81=
n82=      ; SI LE NOMBRE DE TICKET EST TOUJOURS SUPPERIEUR A 0 APRES VERIF GILS
n83=      if (%raffle.viewer. [ $+ [ %target ] ] > 0) {
n84=        ; RETIRE LES GILS ET AJOUTE LE VIEWER A LA TRUE LIST 
n85=        set %profile.gils. [ $+ [ %target ] ] $calc($int(%profile.gils. [ $+ [ %target ] ]) - $int(%prix))
n86=        set %raffle.truelist $iif(%raffle.truelist == $null, %target, %raffle.truelist %target)
n87=        ; INCREMENTE LE NOMBRE DE TICKET TOTAL
n88=        set %raffle.total.ticket $calc($int(%raffle.total.ticket) + $int(%raffle.viewer. [ $+ [ %target ] ]))
n89=      }       
n90=      ; FIN DU IF TICKET > 0
n91=    }
n92=    set %raffle.total.gils $calc($int(%raffle.total.ticket) * $int(%raffle.gils))
n93=    saveViewer %target
n94=    ; FIN DU WHILE
n95=  }
n96=}
n97=
n98=alias raffleDraw {
n99=  ; STOP SI LA RAFFLE EST OUVERTE
n100=  if (%raffle.status != 2) { dire [RAFFLE] Aucune raffle fermé. | return false }
n101=
n102=  ; REGLE LA POSITION DU DRAW 
n103=  if (%raffle.winner.pos == $null) { set %raffle.winner.pos 0 }
n104=  debug raffle.winner.pos: %raffle.winner.pos
n105=
n106=  ; SORT LE RANDOM NOMBRE
n107=  var %rand = $rand(1,$int(%raffle.total.ticket))
n108=  debug rand: %rand
n109=
n110=  ; VARIABLE TEMPORAIRE PENDANT LA BOUCLE
n111=  unset %raffle.draw.waitlist
n112=  set %raffle.draw.temp.total.ticket 0
n113=  var %pos = 0
n114=  var %isWinner = 0
n115=  unset %raffle.pool.start
n116=  unset %raffle.pool.end
n117=
n118=  while (true) {
n119=    ; CHOPE LE VIEWER
n120=    inc %pos
n121=
n122=    ; REGLE LE TARGET OU STOP SI AUCUNE
n123=    if ($ [ $+ [ %pos ] ] == $null) { break }
n124=    else { var %target = $ [ $+ [ %pos ] ] }
n125=    debug target: %target
n126=
n127=    ; REGLE LA PLAGE DE TICKET DE TARGET
n128=    if (%raffle.pool.start == $null) { set %raffle.pool.start 1 }
n129=    else { set %raffle.pool.start $calc($int(%raffle.pool.end) + 1) }
n130=    set %raffle.pool.end $calc($int(%raffle.pool.start) + $int(%raffle.viewer. [ $+ [ %target ] ]) - 1)
n131=    debug ticketpool: %raffle.pool.start à %raffle.pool.end
n132=
n133=    if ((%rand >= $int(%raffle.pool.start)) && (%rand <= $int(%raffle.pool.end))) {
n134=      ; TICKET GAGNANT
n135=      inc %raffle.winner.pos
n136=      set %raffle.winner. [ $+ [ %raffle.winner.pos ] ] %target
n137=      incViewerGils %target %raffle.reward.gils
n138=      var %xpgain = $int($calc($int($paraXPReq(%profile.level. [ $+ [ %target ] ]))*%raffle.reward.xppercent))
n139=      incViewerXP %target %xpgain
n140=      var %isWinner = 1
n141=    }
n142=    else {
n143=      ; TICKET PERDANT
n144=      %raffle.draw.temp.total.ticket $calc($int(%raffle.draw.temp.total.ticket) + $int(%raffle.viewer. [ $+ [ %target ] ]))
n145=      set %raffle.draw.waitlist $iif(%raffle.draw.waitlist == $null, %target, %raffle.draw.waitlist %target)
n146=    }
n147=  }
n148=  ; DEFINIE LE NOUVEUA NOMBRE DE TICKET GAGNANT ET REFAIT LA TRUE LIST
n149=  set %raffle.total.ticket $int(%raffle.draw.temp.total.ticket)
n150=  set %raffle.truelist %raffle.draw.waitlist
n151=
n152=
n153=  ; INCREMENTE LA POSITION SI AUCUN GAGNANT
n154=  if (%isWinner == 0) { inc %raffle.winner.pos }
n155=  ; REMET LE COMPTEUR A 1 SI AUCUN GAGNANT SUR CETTE POS
n156=  if (%raffle.winner. [ $+ [ %raffle.winner.pos ] ] == $null) { set %raffle.winner.pos 1 }
n157=  dire [RAFFLE] Gagnant n $+ %raffle.winner.pos $+ : %raffle.winner. [ $+ [ %raffle.winner.pos ] ] $+ . ( $+ %raffle.reward.gils gils & 20% d'XP $+ )
n158=}
