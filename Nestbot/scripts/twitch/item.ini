[script]
n0=ON *:TEXT:!item*:%twitch.chan:{
n1=  addViewer $nick
n2=  if ($2 == $null) { itemList }
n3=}
n4=
n5=ON *:TEXT:!buy*:%twitch.chan:{
n6=  addViewer $nick
n7=  ; VERIFIE LE LEVEL ET LA SYNTAX
n8=  if (($int(%profile.para.level. [ $+ [ $nick ] ]) < 10) && ($int(%profile.reborn. [ $+ [ $nick ] ]) != 2)) { dire [ITEM] level requis: 10 | return false }
n9=  if (($2 == $null) || ($int($2) != $2)) { dire [ITEM] !buy <numéro> ex: !buy 1 | return false }
n10=
n11=  ; VERIFIE QUE LITEM EXISTE
n12=  var %iName = $2 $+ .name
n13=  if (%item. [ $+ [ %iName ] ] == $null) { dire [ITEM] item introuvable. | return false }
n14=
n15=  ; VERIFIE SI LITEM PEUX ETRE ACHETé (8 = anneau follow)
n16=  var %iGils = $2 $+ .gils
n17=  if (($2 != 8) && ($2 != 9) && ($2 != 10)) {
n18=    if (%item. [ $+ [ %iGils ] ] < 0) { dire [ITEM] cet item ne s'achète pas. | return false }
n19=  }
n20=
n21=  ; VERIFIE QUE LE VIEWER NA PAS LITEM
n22=  var %iViewer = $2 $+ . $+ $nick
n23=  if (%profile.item. [ $+ [ %iViewer ] ] != $null) { dire [ITEM] vous possédez déjà l'item. | return false }
n24=
n25=  ; VERIFIE QUE LE VIEWER A LES GILS
n26=  if ($int(%profile.gils. [ $+ [ $nick ] ]) < $int(%item. [ $+ [ %iGils ] ])) { dire [ITEM] vous n'avez pas les gils. | return false }
n27=
n28=  ; ACHETE LITEM (SAUF ANNEAU FOLLOW)
n29=  if (($2 != 8) && ($2 != 9) && ($2 != 10)) {
n30=    set %profile.gils. [ $+ [ $nick ] ] $calc($int(%profile.gils. [ $+ [ $nick ] ]) - $int(%item. [ $+ [ %iGils ] ]))
n31=    set %profile.item. [ $+ [ %iViewer ] ] 1
n32=    dire [ITEM] %item. [ $+ [ %iName ] ] acheté et équipé
n33=  }
n34=
n35=  ; ANNEAU INFERNAL
n36=  if ($2 == 8) {
n37=    if (%profile.follow. [ $+ [ $nick ] ] == $null) { dire [ITEM] Vous devez follow le stream et attendre 2 minutes. | return false }
n38=    else {
n39=      set %profile.item. [ $+ [ %iViewer ] ] 1
n40=      dire [ITEM] %item. [ $+ [ %iName ] ] acheté et équipé, merci de votre soutiens :)
n41=    }
n42=  }
n43=
n44=  ; ANNEAU FACEBOOK
n45=  if ($2 == 9) {
n46=    if (%profile.fblike. [ $+ [ $nick ] ] == $null) { dire [ITEM] http://facebook.com/nesstream Vous devez like la page FB et inscrire en message votre compte twitch pour obtenir l'anneau ( $+ %item.9.puissance $+ P %item.9.defense $+ D). }
n47=    else {
n48=      set %profile.item. [ $+ [ %iViewer ] ] 1
n49=      dire [ITEM] %item. [ $+ [ %iName ] ] acheté et équipé, merci de votre soutiens :)
n50=    }
n51=  }
n52=
n53=  ; ANNEAU RENAISSANCE
n54=  if ($2 == 10) {
n55=    ; RECUPERE LA VARIABLE profile.reborn
n56=    if (%profile.reborn. [ $+ [ $nick ] ] == $null) { var %reborn = 0 }
n57=    else { var %reborn = $int(%profile.reborn. [ $+ [ $nick ] ]) }
n58=
n59=    if (%reborn < 2) { dire [ITEM] %item. [ $+ [ %iName ] ] s'obtiens level 100 après la quête de renaissance: !renaissance }
n60=    else {
n61=      set %profile.item. [ $+ [ %iViewer ] ] 1
n62=      dire [ITEM] %item. [ $+ [ %iName ] ] acheté et équipé, félicitation !
n63=    }
n64=  }
n65=
n66=  ; RECALCULE LA PUISSANCE / DEFENSE
n67=  paraSetATKDEF $nick
n68=}
n69=
n70=alias itemList {
n71=  var %i = 0
n72=  var %item.list = [ITEM]
n73=  while (true) {
n74=    %i = $calc($int(%i) + 1)
n75=    var %iName = %i $+ .name
n76=    var %iPuissance = %i $+ .puissance
n77=    var %iDefense = %i $+ .defense
n78=    var %iGils = %i $+ .gils
n79=    if (%item. [ $+ [ %iName ] ] != $null) {
n80=      ; AJOUTE LITEM A LA LISTE SI LE PRIX EST PLUS GRAND QUE 0      
n81=      if (%item. [ $+ [ %iGils ] ] > 0) {
n82=        ; ITEM NORMAL
n83=        %item.list = %item.list $chr(91) $+ %i $+ $chr(35) $+ %item. [ $+ [ %iName  ] ] %item. [ $+ [ %iPuissance ] ] $+ P %item. [ $+ [ %iDefense ] ] $+ D %item. [ $+ [ %iGils ] ] $+ G $+ $chr(93) -
n84=      }
n85=      ; ANNEAU FOLLOW
n86=      if (%i == 8) {
n87=        %item.list = %item.list $chr(91) $+ %i $+ $chr(35) $+ %item. [ $+ [ %iName  ] ] 15%XP 0G $+ $chr(93) -
n88=      }
n89=      ; ANNEAU FB
n90=      if (%i == 9) {
n91=        %item.list = %item.list $chr(91) $+ %i $+ $chr(35) $+ %item. [ $+ [ %iName  ] ] %item. [ $+ [ %iPuissance ] ] $+ P %item. [ $+ [ %iDefense ] ] $+ D 0G $+ $chr(93) -
n92=      }
n93=      ; ANNEAU RENAISSANCE
n94=      if (%i == 10) {
n95=        %item.list = %item.list $chr(91) $+ %i $+ $chr(35) $+ %item. [ $+ [ %iName  ] ] 50%XP %item. [ $+ [ %iPuissance ] ] $+ P %item. [ $+ [ %iDefense ] ] $+ D 0G $+ $chr(93) -
n96=      }
n97=    }
n98=    else { break }
n99=  }
n100=  %item.list = %item.list tapez "!buy 1" pour acheter l'Epée.
n101=  dire %item.list
n102=}
