[script]
n0=alias resetRaffle {
n1=  ; ON STOP LA RAFFLE
n2=  unset %raffle
n3=  unset %raffle.ticket.cost
n4=  unset %raffle.ticket.max
n5=  debug-raffle /resetRaffle
n6=}
n7=
n8=alias resetTicket {
n9=  debug-raffle /resetTicket
n10=  ; AVANT UNE NOUVELLE RAFFLE ON VIDE LES TICKET
n11=  unset %profile.raffle.ticket.*
n12=  timerDrawRaffle off
n13=
n14=  ; ON VIDE LES STATS
n15=  unset %raffle.ticket.total
n16=  unset %raffle.gils.total
n17=  unset %raffle.winner.*
n18=  unset %raffle.construct
n19=}
n20=
n21=alias startRaffle {
n22=  ; SI $1 ou $2 NEST PAS UN NOMBRE
n23=  if (($1 != $null) && ($1 != $int($1))) { dire !raffle, bad syntax.. noob! Try: !raffle <ticket cost> <tickets max (optionnal)>, ex: !raffle %raffle.ticket.cost.defaut %raffle.ticket.max.defaut | return false }
n24=  if (($2 != $null) && ($2 != $int($2))) { dire !raffle, bad syntax.. noob! Try: !raffle <ticket cost> <tickets max (optionnal)>, ex: !raffle %raffle.ticket.cost.defaut %raffle.ticket.max.defaut | return false }
n25=
n26=  ; DEFINIE LE PRIX DU TICKET
n27=  if ($1 == $null) { set %raffle.ticket.cost $int(%raffle.ticket.cost.defaut) }
n28=  else { set %raffle.ticket.cost $int($1) }
n29=
n30=  ; DEFINIE LE NOMBRE DE TICKET MAX
n31=  if ($2 == $null) { set %raffle.ticket.max $int(%raffle.ticket.max.defaut) }
n32=  else { set %raffle.ticket.max $int($2) }
n33=
n34=  ; ANNONCE LE DEBUT DE LA RAFFLE PUIS LA DEMARRE
n35=  dire $chr(91) $+ RAFFLE INFO $+ $chr(93) Une tombola va débuter, pour y participer vous devez acheter des tickets en tapant !ticket 5. Cinq étant un exemple, plus vous avez de ticket plus vous avez de chance de gagner. Tapez !ticket 0 pour annuler l'achat avant la fin de la tombola. 
n36=
n37=
n38=  ; LA RAFFLE DEMARE ON RESET LES TICKET PRECEDENT ET LETS GO
n39=  resetTicket
n40=  set %raffle.ticket.total 0
n41=  set %raffle.gils.total 0
n42=  timer 1 5 set %raffle 1
n43=  timer 1 5 dire $chr(91) $+ TOMBOLA DEBUT $+ $chr(93) Prix du ticket: %raffle.ticket.cost - Max tickets/viewer: %raffle.ticket.max
n44=}
n45=
n46=alias stopRaffle {
n47=  resetRaffle
n48=  saveini
n49=  dire $chr(91) $+ TOMBOLA FIN $+ $chr(93) %raffle.ticket.total ticket ont étaient acheté pour un montant de %raffle.gils.total gils
n50=  resetDrawRaffle
n51=}
n52=
n53=alias resetDrawRaffle {
n54=  timerDrawRaffle off
n55=  remove C:\Nestbot\scripts\php\raffle.txt
n56=  unset %raffle.construct
n57=  unset %raffle.winner.*
n58=  set %raffle.winner.nb 0
n59=  timer 1 1 sockopen raffle localhost 80
n60=}
n61=
n62=alias readRaffle {
n63=  ; RECUPERE LA RAFFLE GENERé PAR PHP
n64=  play -c %twitch.chan C:\Nestbot\scripts\php\raffle.txt 1
n65=}
n66=
n67=alias drawRaffle {
n68=  ; FICHIER RAFFLE NON PRET
n69=  if (%raffle.construct == $null) {
n70=    timerDrawRaffle off
n71=    timerDrawRaffle 1 1 drawRaffle
n72=    return false
n73=  }
n74=
n75=  ; DEFINIE LE GAGNANT
n76=  inc %raffle.winner.nb
n77=  var %winner = %raffle.winner. [ $+ [ %raffle.winner.nb ] ]
n78=
n79=  ; SI IL NY A PLUS DE GAGNANT
n80=  if (%winner == $null) { dire $chr(91) $+ DRAW $chr(35) $+ %raffle.winner.nb $+ $chr(93) No more winner | return false }
n81=
n82=  ; LOL ACCOUNT
n83=  if (%profile.lol. [ $+ [ %winner ] ] != $null) { var %lol = %profile.lol. [ $+ [ %winner ] ] }
n84=  else { var %lol = Unknow }
n85=
n86=  ; BTAG
n87=  if (%profile.btag. [ $+ [ %winner ] ] != $null) { var %btag = %profile.btag. [ $+ [ %winner ] ] }
n88=  else { var %btag = Unknow }
n89=
n90=  ;SINON ON ANNONCE LE GAGNANT
n91=  dire $chr(91) $+ DRAW $chr(35) $+ %raffle.winner.nb $+ $chr(93) %winner with %profile.raffle.ticket. [ $+ [ %winner ] ] tickets purchased. (LoL account: %lol $+ , Btag: %btag $+ )
n92=}
n93=
n94=on *:sockopen:raffle:{
n95=  debug-raffle sockopen
n96=  var %ver1 var=lol
n97=  sockwrite -n $sockname POST /stream/raffle.php HTTP/1.1
n98=  sockwrite -n $sockname Content-Type: application/x-www-form-urlencoded
n99=  sockwrite -n $sockname Content-Length: $calc($len(%ver1) +1)
n100=  sockwrite -n $sockname host: localhost
n101=  sockwrite -n $sockname
n102=  sockwrite -n $sockname %ver1
n103=}
n104=on *:sockread:raffle:{ 
n105=  debug-raffle sockread & close
n106=  sockclose raffle
n107=  timer 1 1 readRaffle
n108=  drawRaffle
n109=}
n110=
n111=alias buyTicket {
n112=  ; PREPARE LES VARIABLE
n113=  var %target = $1
n114=  var %ticket = $2
n115=
n116=  ; REFUND VIEWER PAR SECURITEE AVANT DE COMMENCER
n117=  refundTicket %target
n118=
n119=  ; CREER USER SI NEXISTE PAS ET STOP SI IL NEXISTE PAS
n120=  if (%target ison %twitch.chan) { addViewer %target }
n121=  if (%profile.gils. [ $+ [ %target ] ] == $null) { return false }
n122=
n123=  ; CONTROL LE NOMBRE DE TICKET DEMANDER
n124=  if ($int(%ticket) > $int(%raffle.ticket.max)) { var %ticket = $int(%raffle.ticket.max) }
n125=  if (($int(%ticket) == 0) || ($int(%ticket) == $null) || ($int(%ticket) < 0)) { return false }
n126=
n127=  ; CALCULE LE PRIX ET VERIFIE LES GILS PAR RAPPORT AU NOMBRE DE TICKET
n128=  var %cost = $calc($int(%ticket)*$int(%raffle.ticket.cost))
n129=
n130=  ; PAS ASSE DE GILS ON REDEFINI LE NOMBRE DE TICKET
n131=  if ($int(%cost) > $int(%profile.gils. [ $+ [ %target ] ])) {
n132=    var %ticket = $floor($int(%profile.gils. [ $+ [ %target ] ])/$int(%raffle.ticket.cost))
n133=    if (%ticket == 0) { return false }
n134=    var %cost = $calc($int(%ticket)*$int(%raffle.ticket.cost))
n135=  }
n136=
n137=  ; SAUVEGARDE LE NOMBRE DE TICKET VIEWER ET TOTAL
n138=  set %profile.raffle.ticket. [ $+ [ %target ] ] %ticket
n139=  set %raffle.ticket.total $calc($int(%raffle.ticket.total)+$int(%ticket))
n140=
n141=  ; DEDUIT LES GILS ET AJOUTE LE COMPTE AU GILS TOTAL
n142=  set %profile.gils. [ $+ [ %target ] ] $calc($int(%profile.gils. [ $+ [ %target ] ])-$int(%cost))
n143=  set %raffle.gils.total $calc($int(%raffle.gils.total)+$int(%cost))
n144=
n145=  ; DEBUG
n146=  ;raffleDebug %target
n147=}
n148=
n149=alias refundTicket {
n150=  ; DEFINIE LES VARIABLE
n151=  var %target = $1
n152=
n153=  ; CREER USER SI NEXISTE PAS ET STOP SI IL NEXISTE PAS
n154=  if (%target ison %twitch.chan) { addViewer %target }
n155=  if (%profile.gils. [ $+ [ %target ] ] == $null) { return false }
n156=
n157=  ; STOP SI USER NA PAS DE TICKET
n158=  if ((%profile.raffle.ticket. [ $+ [ %target ] ] == $null) || (%profile.raffle.ticket. [ $+ [ %target ] ] < 1)) { return false }
n159=
n160=  ; CALCULE LE PRIX
n161=  var %cost = $calc($int(%raffle.ticket.cost)*$int(%profile.raffle.ticket. [ $+ [ %target ] ]))
n162=
n163=  ; RENDS LES GILS ET LES ENLEVE DES GILS TOTAL
n164=  set %profile.gils. [ $+ [ %target ] ] $calc($int(%profile.gils. [ $+ [ %target ] ])+$int(%cost))
n165=  set %raffle.gils.total $calc($int(%raffle.gils.total)-$int(%cost))
n166=
n167=  ; REPRENDS LES TICKET ET LES ENLEVE DES TICKET TOTAL
n168=  set %raffle.ticket.total $calc($int(%raffle.ticket.total)-$int(%profile.raffle.ticket. [ $+ [ %target ] ]))
n169=  unset %profile.raffle.ticket. [ $+ [ %target ] ]
n170=
n171=  ; DEBUG
n172=  ;raffleDebug %target
n173=}
n174=
n175=alias raffleDebug {
n176=  var %target = $1
n177=  dire <DEBUG> Ticket total acheté: %raffle.ticket.total -- Gils total dépensé: %raffle.gils.total
n178=
n179=  if (%profile.raffle.ticket. [ $+ [ %target ] ] == $null) { var %ticket = 0 }
n180=  else { var %ticket = %profile.raffle.ticket. [ $+ [ %target ] ] }
n181=
n182=  dire <DEBUG> %target $+ : %ticket Tickets et %profile.gils. [ $+ [ %target ] ] restant
n183=}
