[script]
n0=alias makeWhoSetVars {
n1=  ; DEFINIE LES VARIABLE AVANT LE WHO
n2=
n3=  ; SI ADDGILS.ALL NEXISTE PAS, AUCUN MONTANT DE GILS GENERAL
n4=  if ((%addGils.all == $null) || (%addGils.all != $int(%addGils.all))) { set %addGils.all 0 }
n5=  ; SI ADDGILS.PERMIN AUCUN MONTANT DE GILS LIÃ© A LA MINUTE
n6=  if ((%addGils.permin == $null) || (%addGils.permin != $int(%addGils.permin))) { set %addGils.permin 0 }
n7=
n8=  ; VERIFIE SI ON DOIS INCREMENTER 1 MIN A TOUT LE MONDE
n9=  if ((%addTime == $null) || (%addTime != $int(%addTime))) { set %addTime 0 }
n10=
n11=  ; VERIFIE SI CERTAIN VIEWER SONT CREDITER EN GILS
n12=  if ((%addGils.viewer == $null) || (%addGils.viewer != $int(%addGils.viewer))) { set %addGils.viewer 0 }
n13=
n14=  ; VERIFIE SI ON DOIS INCREMENTER DE L'XP
n15=  if ((%addXp.permin == $null) || (%addXp.permin != $int(%addXp.permin))) { set %addXp.permin 0 }
n16=
n17=  ; VERIFIE SI ON DOIS AJOUTER L'XP DUN RUN A TOUT LES USER
n18=  if ((%addRun.all == $null) || (%addRun.all != $int(%addRun.all))) { set %addRun.all 0 }
n19=
n20=  ; VERIFIE SI ON DOIS AJOUTER L'XP DUNE GAME LOL
n21=  if ((%addLol.all == $null) || (%addLol.all != $int(%addLol.all))) { set %addLol.all 0 }
n22=
n23=  ; VIDE LA LISTE ONLINE/MODO
n24=  isonViewerClear
n25=  isopViewerClear 
n26=}
n27=
n28=alias makeWhoUnsetVars {
n29=  unset %addXp.permin
n30=  unset %addGils.all
n31=  unset %addGils.permin
n32=  unset %addTime
n33=  unset %addGils.viewer
n34=  unset %addGils.viewer.*
n35=  unset %addGils.total
n36=  unset %addRun.all
n37=  unset %addLol.all
n38=  unset %isNeeded
n39=}
n40=
n41=alias makeWho {
n42=  ; REGLE LES VARIABLE OBLIGATOIRE SI ELLE NEXISTE PAS OU NE SONT PAS DES INTEGER
n43=  makeWhoSetVars
n44=
n45=  ; VERIFIE SI LE /WHO EST NECESSAIRE
n46=  var %isNeeded = 0
n47=  if (%addGils.all != 0) { var %isNeeded = 1 }
n48=  if (%addGils.permin != 0) { var %isNeeded = 1 }
n49=  if (%addTime != 0) { var %isNeeded = 1 }
n50=  if (%addGils.viewer != 0) { var %isNeeded = 1 }
n51=  if (%addXp.permin != 0) { var %isNeeded = 1 }
n52=  if (%addRun.all != 0) { var %isNeeded = 1 }
n53=  if (%addLol.all != 0) { var %isNeeded = 1 }
n54=
n55=  ; RESULTAT DE LA VERIF CI-DESSUS
n56=  if (%isNeeded == 0) { makeWhoUnsetVars | return false }
n57=
n58=  ; ON CONTINUE SEULEMENT SI LE /WHO ETAIT NECESSAIRE
n59=  debug-permin /makeWho START
n60=  debug-permin var addGils.all = %addGils.all
n61=  debug-permin var addTime = %addTime
n62=  debug-permin var addGils.viewer = %addGils.viewer
n63=  debug-permin var addXp.permin = %addXp.permin
n64=  debug-permin var addRun.all = %addRun.all
n65=  debug-permin var addLol.all = %addLol.all
n66=
n67=  ; CALCULE LES GILS TOTAL POUR TOUT LES USER
n68=  set %addGils.total %addGils.all + %addGils.permin
n69=  debug-permin var addGils.total = %addGils.total
n70=
n71=  ; VERIFIE QUE LA DATE EST BONNE
n72=  setToday
n73=
n74=  ; PVE PROC OU PAS TOUTE LES MINUTE SEULEMENT
n75=  if ($int(%addTime) > 0) { PVEPermin }
n76=
n77=  ; SIMULE UN WHO EN PARCOURANT TOUT LES PSEUDO DANS LE CHAT
n78=  set %who.nb 0
n79=  while ($true) {
n80=    ; DEFINIE LA TARGET ET STOP SI ELLE NEXISTE PAS
n81=    inc %who.nb
n82=    var %target = $nick(%twitch.chan,%who.nb)
n83=    if (%target == $null) { break }
n84=
n85=    ; NEWER USER SEULEMENT SI IL NEXISTE PAS
n86=    addViewer %target
n87=
n88=    ; AJOUTE LE TIME
n89=    incViewerTime %target %addTime
n90=
n91=    ; AJOUTE LES GILS
n92=    incViewerGils %target %addGils.total
n93=
n94=    ; AJOUTE L'XP PAR MINUTE ET DU RUN
n95=    set %addXp.total $calc( $int(%addXp.permin) + $int(%addRun.all) )
n96=
n97=    ; AJOUTE L'XP DUNE GAME LOL
n98=    set %addXp.total $calc( $int(%addXp.total) + $int(%addLol.all) )
n99=
n100=    paraAddXp %target %addXp.total
n101=
n102=    ; LANCE LE CALCULE DU LEVEL
n103=    paraSetLevel %target
n104=
n105=    ; AJOUTE LES GILS A UN SEUL USER
n106=    if (%addGils.viewer. [ $+ [ %target ] ] != $null) { incViewerGils %target %addGils.viewer. [ $+ [ %target ] ] }
n107=    else { set %addGils.viewer. [ $+ [ %target ] ] 0 }
n108=
n109=    ; VERIFIE SI LUSER EST ISOP ET ONLINE
n110=    isopViewer %target
n111=    isonViewer %target
n112=
n113=    ; AFFICHE LE RESULTAT EN DEBUG
n114=    debug-permin %target $+ : + %addGils.total GILS + %addTime TIME + %addGils.viewer. [ $+ [ %target ] ] GILS + %addXp.permin XP + %addRun.all XPRun + %addLol.all XPLol ( %addXp.total XP TOTAL )
n115=    unset %addGils.viewer. [ $+ [ %target ] ]
n116=  }
n117=  unset %who.nb
n118=
n119=  ; END OF WHO
n120=  makeWhoUnsetVars
n121=  debug-permin /makeWho END
n122=
n123=  ; SAVE LE FICHIER VARS.INI
n124=  debug-permin /saveini
n125=  saveini
n126=}
